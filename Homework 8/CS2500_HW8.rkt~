;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-lambda-reader.ss" "lang")((modname |CS2500 HW8|) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f () #f)))
(require "./hashtable-extras.rkt")
(require "./crypto-extras.rkt")
(require "./http-extras.rkt")

;; A [Hash-Table-of X Y] is a (make-hash (list (list X Y) ...))
;; where X is the key and Y is the value that the key maps to

;; make-hash : [List-of [List-of-Two X Y]] -> [Hash-table-of X Y]
;; Creates a hash table from a list of pairs.

;; hash-has-key? : [Hash-table-of X Y] X -> Boolean
;; Checks if a hash table has a key. Returns #true if it does, #false otherwise.

;; hash-ref : [Hash-table-of X Y] X -> Y
;; Returns the value associated with the key in the hash table.

;; hash-set : [Hash-table-of X Y] X Y -> [Hash-table-of X Y]
;; Returns a new hash table with the key mapped to the value.

;; hash-remove : [Hash-table-of X Y] X -> [Hash-table-of X Y]
;; Returns a new hash table with the key removed

;; hash-keys : [Hash-table-of X Y] -> [List-of X]
;; Returns a list of all the keys in the hash table

;; hash-values : [Hash-table-of X Y] -> [List-of Y]
;; Returns a list of all the values in the hash table

;; =================================

;; A PrivateKey is a String that represents a 512-bit RSA private key.

;; A PublicKey is a String that represents a 512-bit RSA public key.

;; A Signature is a String that represents a 512-bit RSA signature.

;; digest : String -> Nat
;; Produces the SHA256 digest of the given string. SHA256 is a cryptographic
;; hash function that is used in many blockchains, and we will use it too.

;; secret->public : PrivateKey -> PublicKey
;; Generates a public key from a private key.

;; make-signature : String PrivateKey -> Signature
;; Signs a string with a private key.

;; check-signature : PublicKey String Signature -> Boolean
;; Checks if the given string was signed by the given public key.

;; =================================

;; An [Optional X] is one of:
;; - X
;; - #false
;;
;; Interpretation: Either a value of type X or #false

;; =================================

(define-struct transaction [serial unique-string sender-sig sender-key receiver-key amount])
;; A Transaction is a (make-transaction Nat String Signature PublicKey PublicKey Nat)
;;
;; (make-transaction serial unique-string sender-sig sender-key receiver-key amount) represents a
;; single transaction that moves amount accelcoin from sender-key to receiver-key.
;; Moreover:
;;
;; 1. The amount must be positive;
;; 2. The unique-string must be globally unique;
;; 2. The signature signs the string 
;;      (string-append unique-string receiver-key ":" (number->string amount))
;;    with the private key corresponding to sender-key.
;; 3. the unique-string is a string that is unique to this transaction.

(define-struct block [transactions nonce miner-key])
;; A Block is a (make-block [List-of Transaction] Nat PublicKey)
;;
;; (make-block transactions nonce miner-key) represents a block of transactions mined by miner-key.
;; The transactions are processed left-to-right. Thus (first transactions) occurs before
;; (second transactions).

;; A Blockchain is a [NE-List-of Block]
;;
;; The first element of a Blockchain is the latest block and the last element is the first
;; block or the *genesis block*. The genesis block has zero transactions and all other blocks have
;; three or more transactions.

(define ALICE-PRIVATE-KEY
  (string-append "MIIBOgIBAAJBAMrPOfefdvowOwAplxY/NLkJFymyedikvwvsyhtQ98CawNXeKydg+WYD9YzQ"
                 "W1tIY5Ta1bqZhk5hpWGM4eusKxkCAwEAAQJAMtQW2hmsLu3xi4vg4uF6bDl8BaZGZWZ8vxdc"
                 "W9ZCEZIEtnYGlkpwoG5YcUp3a39YRP+Nt2fA6bmPbvjmWAspkQIhAPodYjlh0p7P4QodsvQi"
                 "nMRp9Z8knfBmYeQpg/0otBMVAiEAz5Tjyw0Yanh6CCIvDRKQ+SvdTMvrJykIMyzmsWgYSPUC"
                 "IEwGvIG2w3/0rnIVvvzIvKBTmQ7L4ZpedKkXGYDNa5dVAiAfRL5Lh911rFA1iXCs927/Gaxs"
                 "NQtnCrdBfjIB5zxBQQIhAO0ZN+PGdjJfbhivUdgfx+DbrHkClSWT8SidILAbgQkd"))
(define BOB-PRIVATE-KEY
  (string-append "MIIBOwIBAAJBAKy4zO2w1HfXMNHSCYKuheD+5ZkAlHubePYNOVvi3gA/AQ1S0HcRFmTk"
                 "zFz/SCp+0cZ3wErzHhKXmvgIrjLbdYMCAwEAAQJACBwBGyPTRfEnjKJk6erRxFeTZhS"
                 "d5BPPoRXL3KGRNMesv5qct9QNbHA2ghjY4Z1gokwLgCViG88FvG0qMKGNSQIhANduvtUGG"
                 "vqeb+c6khwi60sf/3KMa082IjC3fe4RosJPAiEAzT8eusKDsL3q38i1o6E4pzUuW4oK0ta1BCGEdZn"
                 "2kI0CIDb6bz8ECNyOlHZJL0J48t1ANDuydCxJ313ZZgzceVHnAiEApVA7vg"
                 "1B6K9vaIPO2VbXvMW26wAKq7tH3WXpvJcf41kCIQCTv8zWOp8Dq3NKTdFZD2"
                 "8NCohpiEOAP3yMng9HhXcAqg=="))
(define ALICE-PUBLIC-KEY (secret->public ALICE-PRIVATE-KEY))
(define BOB-PUBLIC-KEY (secret->public BOB-PRIVATE-KEY))

(define PRIVATE-KEY-1 "MIIBOwIBAAJBAK59eNiP8RRwaa0y34piNlvYOyFDXtRf8NtneIoL8JMMvfVfH6X9a/zzIxHKxIlcY7WZCMVCN7+yU2L0uFE8zT8CAwEAAQJAR3HUk0eMvIOyoVodMXo5K64DuADvySFna06Yil2NKYf2iG6wttj5I2LoPHnK2EfaGGXelt1NBBCTobiJvlGfgQIhAOO22GUjOzNeG2iJO+DsUOijB5poPYIuP3sxBXPJLIpfAiEAxCojCtL0vvExVUylVYC9/f+8d37s0meNXnXIT5cCaSECIQCTJh7kDEY/RXrUrInjKRKHooJRRzcoHparFtx9G+0KZQIgO2+M+I2cjHImAXNcsQB2EIy4/PffJ40qn1FOpRbbKyECIQC9GErNEnR1dWL66hlxqDr67drQghpBgPkcKGYWW4qY7Q==")

(define PUBLIC-KEY-1 (secret->public PRIVATE-KEY-1))

;; build-transaction: Nat PrivateKey PublicKey Nat -> Transaction
;; (build-transaction serial sender-private-key receiver-public-key amount) builds a transaction
;; that sends amount from the sender to the receiver.
(define (build-transaction serial priv pub amt)
  (local
    ([define str (unique-string)])
    (make-transaction serial
                      str
                      (make-signature (string-append str pub ":" (number->string amt)) priv)
                      (secret->public priv)
                      pub
                      amt)))

;; Sends 100 accelcoins from Alice to Bob
(define EX-TRANSACTION-0 (build-transaction 0 ALICE-PRIVATE-KEY BOB-PUBLIC-KEY 100))

;; transaction->string : Transaction -> String
;; Serializes a transaction into a string with the format
;; "transaction:unique-string:sender-sig:sender-key:receiver-key,amount"
(define (transaction->string t)
  (string-append "transaction:"
                 (transaction-unique-string t)
                 ":"
                 (transaction-sender-sig t)
                 ":"
                 (transaction-sender-key t)
                 ":"
                 (transaction-receiver-key t)
                 ","
                 (number->string (transaction-amount t))))

;; block-digest: Digest Block -> Digest
;; (block-digest prev-digest block) computes the digest of block, given the digest
;; of the previous block.
;;
;; The digest must be the digest of the following strings concatenated in order:
;;
;; 1. prev-digest as a string
;; 2. The transactions as strings (using transaction->string) concatenated in order
;; 3. The nonce as a string
(define (block-digest prev-digest block)
  (digest
   (string-append
    (number->string prev-digest)
    (foldr (lambda (x y) (string-append (transaction->string x) y)) "" (block-transactions block))
    (number->string (block-nonce block)))))

;; The limit digest value
(define DIGEST-LIMIT (expt 2 (* 8 30)))

(check-expect (< (block-digest 0 EX-BLOCK-1) DIGEST-LIMIT) #true)

;; mine-block : Digest PublicKey [List-of Transaction] Nat -> [Optional Block]
;; (mine-block prev-digest miner-public-key transactions trials)
;; tries to mine a block, but gives up after trials attempts.
;;
;; The produced block has a digest that is less than DIGEST-LIMIT.
(define (mine-block prev-digest miner-public-key transactions trials)
  (cond
    [(= trials 0) #false]
    [else
     (local [(define generated-block (make-block transactions (random 4294967087) miner-public-key))]
       (if (< (block-digest prev-digest generated-block) DIGEST-LIMIT)
           generated-block
           (mine-block prev-digest miner-public-key transactions (- trials 1))))]))

;; A genesis block where Alice starts the blockchain and receives the first mining reward.
(define EX-BLOCK-0
  (make-block '()
              8631727707325622792404128232286945630015639849891523695238049493932286431978
              ALICE-PUBLIC-KEY))

;; Example block 1 that links to the previous the genesis block
(define EX-BLOCK-1
  (mine-block 0
              ALICE-PUBLIC-KEY
              (list (build-transaction 0 ALICE-PRIVATE-KEY BOB-PUBLIC-KEY 10)
                    (build-transaction 1 BOB-PRIVATE-KEY ALICE-PUBLIC-KEY 10)
                    (build-transaction 2 ALICE-PRIVATE-KEY BOB-PUBLIC-KEY 10)
                    (build-transaction 3 BOB-PRIVATE-KEY ALICE-PUBLIC-KEY 10)
                    (build-transaction 4 ALICE-PRIVATE-KEY BOB-PUBLIC-KEY 10))
              1000000))

;; Example valid blockchain with EX-BLOCK-0 as the genesis and EX-BLOCK-1 as the after
(define EX-BC-0 (list EX-BLOCK-1 EX-BLOCK-0))

;; Example invalid blockchain with EX-BLOCK-1 as the genesis and EX-BLOCK-0 as the after
(define EX-BC-1 (list EX-BLOCK-0 EX-BLOCK-1))

;; -------------Blockchain validation------------------------
;; blocks-sized-ok? : Blockchain -> Boolean
;; Determines that every block has at least three transactions, and that
;; the genesis block has zero transactions.
(define (blocks-sized-ok? bc)
  (and (empty? (block-transactions (first (reverse bc))))
       (andmap (lambda (x) (>= (length (block-transactions x)) 3)) (rest (reverse bc)))))

(check-expect (blocks-sized-ok? EX-BC-0) #true)
(check-expect (blocks-sized-ok? EX-BC-1) #false)

;; map-transactions : (Transaction -> X) Blockchain -> [List-of X]
;; Transaction-level map for blockchains.
(define (map-transactions f bc)
  (foldr (lambda (x y) (append (map f (block-transactions x)) y)) empty bc))

(check-expect (map-transactions transaction-serial EX-BC-0) (list 0 1 2 3 4))
(check-expect (map-transactions transaction-amount EX-BC-0) (list 10 10 10 10 10))

;; no-duplicate-transactions? : Blockchain -> Boolean
;; Determines if every transaction in the blockchain appears exactly once. Every
;; transaction has a unique serial number that we use to determine if it is unique.
(define (no-duplicate-transactions? bc)
  (local ((define serial-list (sort (map-transactions transaction-serial bc) <)))
    (not (ormap equal? serial-list (append (rest serial-list) (list null))))))

(check-expect (no-duplicate-transactions? EX-BC-0) #true)
(check-expect (no-duplicate-transactions? EX-BC-1) #true)

;; all-signatures-ok? : Blockchain -> Boolean
;; Determines if every transaction in the blockchain has a valid signature.
(define (all-signatures-ok? bc)
  (andmap (lambda (x)
            (check-signature
             (transaction-sender-key x)
             (string-append
              (transaction-unique-string x)
              (transaction-receiver-key x)
              ":"
              (number->string (transaction-amount x)))
             (transaction-sender-sig x)))
          (map-transactions identity bc)))

(check-expect (all-signatures-ok? EX-BC-0) #true)
(check-expect (all-signatures-ok? EX-BC-1) #true)

;; valid-digests? : Blockchain -> Boolean
;; Determines if every block has a valid digest.
(define (valid-digests? Blockchain)
  (local [;; valid-digests-helper : Blockchain -> [Optional Digest]
          ;; (valid-digests-helper bc) produces #false if any block has an invalid digest.]
          (define (valid-digests-helper bc)
            (cond
              [(empty? (rest bc)) 0]
              [else
               (if (boolean? (valid-digests-helper (rest bc)))
                   #false
                   (if (< (block-digest (valid-digests-helper (rest bc)) (first bc)) DIGEST-LIMIT)
                       (valid-digests-helper (rest bc))
                       #false))]))]
    (not (boolean? (valid-digests-helper Blockchain)))))

(check-expect (valid-digests? EX-BC-0) #true)
(check-expect (valid-digests? EX-BC-1) #false)

;; account-balances-ok? : Blockchain -> Boolean
;; Determines if every transaction sends at least 1 accelcoin and every sender
;; always has enough accelcoin to send.
(define (account-balances-ok? bc)
  (not (false? (update-ledger/blockchain bc (make-hash '())))))

(check-expect (account-balances-ok? EX-BC-0) #true)
(check-expect (account-balances-ok? EX-BC-1) #false)

;; hash-update : [Hash-table-of X Y] X (Y -> Y) Y -> [Hash-table of X Y]
;; updates entry using function if present, else default
(define (hash-update h k upd def)
  (hash-set h k (if (hash-has-key? h k) (upd (hash-ref h k)) def)))

(check-expect (hash-update (make-hash (list)) "foo" add1 0) (make-hash (list (list "foo" 0))))
(check-expect (hash-update (make-hash (list (list "foo" 0) (list "bar" 0))) "foo" add1 0)
              (make-hash (list (list "foo" 1) (list "bar" 0))))

;; A Ledger is a [Hash-Table-of PublicKey Nat]
;; A ledger maps wallet IDs (public keys) to the number of accelcoins they have.

(define MINER-REWARD 100)
;; reward : PublicKey Ledger -> Ledger
;; Grants the miner the reward for mining a block.
(define (reward PubKey Ledger)
  (hash-update Ledger PubKey (lambda (x) (+ MINER-REWARD x)) MINER-REWARD))

(check-expect (reward ALICE-PUBLIC-KEY (make-hash '()))
              (make-hash (list (list ALICE-PUBLIC-KEY 100))))
(check-expect (reward ALICE-PUBLIC-KEY (make-hash (list (list ALICE-PUBLIC-KEY 1000))))
              (make-hash (list (list ALICE-PUBLIC-KEY 1100))))

;; update-ledger/transaction: Transaction Ledger -> [Optional Ledger]
;; Updates the ledger with a single transaction. Produces #false if
;; the sender does not have enough accelcoin to send.
(define (update-ledger/transaction tr l)
  (cond
    [(or (not (hash-has-key? l (transaction-sender-key tr)))
         (> (transaction-amount tr) (hash-ref l (transaction-sender-key tr)))
         (<= (transaction-amount tr) 0))
     #f]
    [else
     (hash-update (hash-update l
                               (transaction-receiver-key tr)
                               (lambda (x) (+ x (transaction-amount tr)))
                               (transaction-amount tr))
                  (transaction-sender-key tr)
                  (lambda (x) (- x (transaction-amount tr)))
                  0)]))

(check-expect (update-ledger/transaction EX-TRANSACTION-0 (make-hash '())) #false)
(check-expect
 (update-ledger/transaction EX-TRANSACTION-0 (make-hash (list (list ALICE-PUBLIC-KEY 1000))))
 (make-hash (list (list ALICE-PUBLIC-KEY 900) (list BOB-PUBLIC-KEY 100))))

;; update-ledger/block : Block Ledger -> [Optional Ledger]
;; Updates the ledger with the transactions in a block, and rewards the miner.
;; Produces #false if any transaction in the block would make a sender's
;; balance negative. The miner receives their reward *after* all transactions
;; are procsed.
(define (update-ledger/block b l)
  ;; ledge-helper : [List-of Transaction] Ledger -> [Optional Ledger]
  ;; Given list of transactions, updates ledger with each transaction from right to left
  (local ((define (ledge-helper lot ledge)
            (cond
              [(empty? lot) ledge]
              [else
               (local ((define prev (ledge-helper (rest lot) ledge)))
                 (if (boolean? prev) #f (update-ledger/transaction (first lot) prev)))]))
          (define lh (ledge-helper (reverse (block-transactions b)) l)))
    (if (boolean? lh) #f (reward (block-miner-key b) lh))))

(check-expect (update-ledger/block EX-BLOCK-0 (make-hash '()))
              (make-hash (list (list ALICE-PUBLIC-KEY 100))))
(check-expect (update-ledger/block EX-BLOCK-1 (make-hash '())) false)

;; update-ledger/blockchain : Blockchain Ledger -> [Optional Ledger]
;; Produces the ledger for a blockchain, or #false if any transaction
;; would make the sender's balance negative.
(define (update-ledger/blockchain bc ledge)
  (cond
    [(empty? bc) ledge]
    [else
     (local ((define prev (update-ledger/blockchain (rest bc) ledge)))
       (if (boolean? prev) #f (update-ledger/block (first bc) prev)))]))

(check-expect (update-ledger/blockchain EX-BC-0 (make-hash '()))
              (make-hash (list (list ALICE-PUBLIC-KEY 190) (list BOB-PUBLIC-KEY 10))))

;; valid-blockchain? : Blockchain -> Boolean
;; Determines if a blockchain is valid.
(define (valid-blockchain? bc)
  (and (blocks-sized-ok? bc)
       (no-duplicate-transactions? bc)
       (all-signatures-ok? bc)
       (valid-digests? bc)
       (account-balances-ok? bc)))

(check-expect (valid-blockchain? EX-BC-0) #true)
(check-expect (valid-blockchain? EX-BC-1) #false)

;; =========================
;; NETWORK STUFF

;; send-transaction: PrivateKey PublicKey Nat -> Boolean
;; (send-transaction sender-private-key receiver-public-key amount) sends a
;; transactions to the Accelchain broadcaster.
(define (send-transaction priv pub amt)
  (post-data
   "broadcaster.federico.codes"
   "/"
   (transaction->string (build-transaction 0 priv pub amt))))
